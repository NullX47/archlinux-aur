## Maintainer: khanhas <xuankhanh963@gmail.com>, itsmeow <itsmeow@itsmeow.dev>
_gitname=spicetify-cli
pkgname="${_gitname}-git"
pkgver=2.38.4
pkgrel=2
pkgdesc='Command-line tool to customize Spotify client'
arch=('x86_64' 'i686')
url='https://github.com/spicetify/cli'
license=('LGPL-2.1-only')
makedepends=('git' 'go')
depends=('glibc' 'bash')
optdepends=('xdg-utils: Allows for opening directories in default file manager')
source=("${_gitname}::git+${url}.git")
sha256sums=('SKIP')
provides=("${_gitname}")
conflicts=("${_gitname}")

pkgver() {
  cd "$srcdir/${_gitname}"
  git describe --long --tags | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

build() {
  cd "${_gitname}"
  export GO111MODULE="auto"
  export GOPATH="${srcdir}"
  export CGO_CPPFLAGS="${CPPFLAGS}"
  export CGO_CFLAGS="${CFLAGS}"
  export CGO_CXXFLAGS="${CXXFLAGS}"
  export CGO_LDFLAGS="${LDFLAGS}"
  export GOFLAGS="-buildmode=pie -trimpath -ldflags=-linkmode=external -mod=readonly -modcacherw"
  go build -ldflags="-X 'main.version=${pkgver}'" -o spicetify
}

check() {
  cd "${_gitname}"
  test "v$(./spicetify -v)" = "v${pkgver}" || exit 1
}

package() {
  cd "${_gitname}"
  
  # install everything to /opt
  install -Dm755 ./spicetify "${pkgdir}/opt/${_gitname}/spicetify"
  cp -r ./CustomApps "${pkgdir}/opt/${_gitname}/CustomApps"
  cp -r ./Extensions "${pkgdir}/opt/${_gitname}/Extensions"
  cp -r ./jsHelper "${pkgdir}/opt/${_gitname}/jsHelper"
  cp -r ./Themes "${pkgdir}/opt/${_gitname}/Themes"

  install -Dm755 ./css-map.json "${pkgdir}/opt/${_gitname}/css-map.json"
  install -Dm755 ./globals.d.ts "${pkgdir}/opt/${_gitname}/globals.d.ts"

  # Make shortcut
  install -dm755 "${pkgdir}/usr/bin"
  echo "#!/bin/sh
exec /opt/${_gitname}/spicetify \"\$@\"" > "${pkgdir}/usr/bin/spicetify"
  chmod 755 "${pkgdir}/usr/bin/spicetify"

  # Clean up deps
  go clean -modcache
}
